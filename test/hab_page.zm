

import sound_direction;


// enum SoundDirection {
//     SND_DIRECTION_LEFT  = 1 << 0,
//     SND_DIRECTION_RIGHT = 1 << 1,
//     SND_DIRECTION_FRONT = 1 << 2
// };

// enum SoundChannel {
//     CHANNEL_FRONT_LEFT  = 0,
//     CHANNEL_FRONT_RIGHT = 1,
//     CHANNEL_REAR_LEFT   = 2,
//     CHANNEL_REAR_RIGHT  = 3
// };

Page test_page
{
    TestItem[]  item_list;      // All presentable items.
    TestItem    item;           // the currently presented item.
    int         nth_trial;      // the number of the current trial.

    //Object      caller;

    // This timer is started at the beginning of a trial if it is fired
    // the trial ends because of maximum trial duration is called.
    Timer max_trial_dur_timer
    {
        on_event:expire()
        {
            response.process_hit(event_time, EVENT_STOP_TIMER);
        }
    }
    
    // Pressing the left button on the button box starts this timer
    // if the timer expires the trial will be aborted.
    Timer abort_trial_timer
    {
        on_event:expire()
        {
            response.process_hit(event_time, EVENT_STOP_TIMER);
        }
    }

    // Key press doesn't work properly since the baby window is a output
    // only window.
    on_event:key_press()
    {
        if      (input_key == KEY_Shift_L)
            response.process_hit(event_time, EVENT_STOP_LOOKING);
        else if (input_key == KEY_Shift_R)
            response.process_hit(event_time, EVENT_START_LOOKING);
        else
            handle_special_key(this, input_key, input_modifiers);
    }

    on_event:message()
    {
        response.process_hit(event_time, message_arg);
    }

    Response response
    {
        bool    is_looking;
        time    lookstart;
        time    nolookstart;
        dur     cumlt;
        dur     cumnlt;
        int     nlookaways;
        int     nlooks;

        on_event:start()
        {
            clear();
            lookstart = event_time;
            // assume the child is looking.
            is_looking = true;
        }

        void clear()
        {
            is_looking  = true;
            lookstart   = time(0);
            nolookstart = time(0);
            cumlt       = 0ms;
            cumnlt      = 0ms;
            nlookaways  = 0;
            nlooks      = 1; // The infant is always looking at the start
                             // of the trial.
        }

        void process_hit(time t, int event)
        {
            HitType validity = hit(t);
            //println("Process hit: " + string(validity) + " " + event);
            
            if (validity == HIT_VALID) {
                if (event == EVENT_STOP_LOOKING) { // start abort timer
                    if (!is_looking)
                        return; // ignore, because we weren't looking.
                    is_looking = false;
                    nolookstart = t;
                    cumlt += (t - lookstart);

                    abort_trial_timer.start(t + MAX_LOOK_AWAY_DURATION);
                    control.stop_clock(t);
                    nlookaways++;
                }
                else if (event == EVENT_START_LOOKING) {
                    if(is_looking)
                        return; // ignore, because we already were looking.
                    is_looking = true;
                    lookstart = t;
                    cumnlt += (t - nolookstart);

                    control.start_clock(t);
                    abort_trial_timer.abort();
                    nlooks++;
                }
                else if (event == EVENT_STOP_TIMER) {
                    if (is_looking)
                        cumlt += (t - lookstart);
                    else
                        cumnlt+= (t - nolookstart);

                    // abort trial
                    done(CONTINUE);
                }
            }
        }
    }

    VerticalLayout layout
    {
        on_event:init()
        {
            spacing     = 50;
            height      = 0;
        }

        CanvasGadget canvas
        {
            real max_radius;

            on_event:init()
            {
                size = 600,600;
                offset_x = width/2;
                offset_y = height/2;

                max_radius =
                    canvas.width < canvas.height ? canvas.width : canvas.height;
                max_radius /= 2;
                //Hide the canvas frame
                fill_pattern_color = TEST_PAGE_COLOR;

                // This puts the bullseye in a frame
                //fill_pattern_color = TEST_PAGE_STIMULUS_COLOR;

                is_visible = true;
            }

            on_event:start()
            {
                //println("Releasing clock.");
                control.release_clock(event_time);
            }

            // Make circle shapes that get smaller and smaller.
            CircleShape c0
            {
                on_event:init()
                {
                    x = 0;
                    y = 0;
                    radius = max_radius - max_radius / 8 * 0;
                    fill_pattern_color = shape_colors[0];
                    is_visible = true;
                }
            }

            CircleShape c1
            {
                on_event:init()
                {
                    x = 0;
                    y = 0;
                    radius = max_radius - max_radius / 8 * 1;
                    fill_pattern_color = shape_colors[1];
                    is_visible = true;
                }
            }

            CircleShape c2
            {
                on_event:init()
                {
                    x = 0;
                    y = 0;
                    radius = max_radius - max_radius / 8 * 2;
                    fill_pattern_color = shape_colors[2];
                    is_visible = true;
                }
            }

            CircleShape c3
            {
                on_event:init()
                {
                    x = 0;
                    y = 0;
                    radius = max_radius - max_radius / 8 * 3;
                    fill_pattern_color = shape_colors[3];
                    is_visible = true;
                }
            }

            CircleShape c4
            {
                on_event:init()
                {
                    x = 0;
                    y = 0;
                    radius = max_radius - max_radius / 8 * 4;
                    fill_pattern_color = shape_colors[4];
                    is_visible = true;
                }
            }

            CircleShape c5
            {
                on_event:init()
                {
                    x = 0;
                    y = 0;
                    radius = max_radius - max_radius / 8 * 5;
                    fill_pattern_color = shape_colors[5];
                    is_visible = true;
                }
            }

            CircleShape c6
            {
                on_event:init()
                {
                    x = 0;
                    y = 0;
                    radius = max_radius - max_radius / 8 * 6;
                    fill_pattern_color = shape_colors[6];
                    is_visible = true;
                }
            }

            CircleShape c7
            {
                on_event:init()
                {
                    x = 0;
                    y = 0;
                    radius = max_radius - max_radius / 8 * 7;
                    fill_pattern_color = shape_colors[7];
                    is_visible = true;
                }
            }

        }

        void reset()
        {
            full_abort();
            canvas.is_visible    = false;
            canvas.c0.is_visible = false;
            canvas.c1.is_visible = false;
            canvas.c2.is_visible = false;
            canvas.c3.is_visible = false;
            canvas.c4.is_visible = false;
            canvas.c5.is_visible = false;
            canvas.c6.is_visible = false;
            canvas.c7.is_visible = false;
        }

        void start_stimulus(time t)
        {
            canvas.start(t);
            canvas.c0.start(t);
            canvas.c1.start(t);
            canvas.c2.start(t);
            canvas.c3.start(t);
            canvas.c4.start(t);
            canvas.c5.start(t);
            canvas.c6.start(t);
            canvas.c7.start(t);
        }
    }

    SoundChain audio
    {
        SoundFile clip{}
        SoundPlayback playback{}

        SoundDirection dir;

        on_event:start()
        {
            int lights = 0;
            if (dir & SND_DIRECTION_LEFT)
                lights |= LEFT_SIDE;
            if (dir & SND_DIRECTION_FRONT)
                lights |= FRONT_SIDE;
            if (dir & SND_DIRECTION_RIGHT)
                lights |= RIGHT_SIDE;

            control.hide_speaker_icon(FRONT_SIDE | LEFT_SIDE | RIGHT_SIDE);
            control.show_speaker_icon(lights);

            //perhaps save audio start even to detailed log.
        }

        on_event:finish()
        {
            control.hide_speaker_icon(FRONT_SIDE | LEFT_SIDE | RIGHT_SIDE);
            // perhaps save audio end event to detailed log.

            StimOutput output;
            time begin = zero_time();
            output.trial    = nth_trial;
            output.tstart   = int(start_time - begin);
            output.tend     = int(finish_time - begin);
            output.stimulus = clip.file;

            stimulus_output_append(output);

            // start the new audio stimulus
            select_new_stimulus();

            // always from front.
            play(event_time + TEST_ISI, SND_DIRECTION_FRONT);
        }

        void select_new_stimulus()
        {
            item = item_list[random(0, item_list.size)];
        }

        time play(time t, SoundDirection direction)
        {
            abort();
            dir = direction;

            playback.set_scaling(0.0); // all channels off.

            // Turn scaling back on for the proper channels
            // keep in mind that the front right isn't wired (2017)
            // and the front left represents the front.
            if (dir & SND_DIRECTION_FRONT) {
                playback.set_scaling(CHANNEL_FRONT_LEFT , 1.0);
                playback.set_scaling(CHANNEL_FRONT_RIGHT, 1.0);
            }
            else {
                playback.set_scaling(CHANNEL_FRONT_LEFT , 0.0);
                playback.set_scaling(CHANNEL_FRONT_RIGHT, 0.0);
            }

            if (dir & SND_DIRECTION_LEFT)
                playback.set_scaling(CHANNEL_REAR_LEFT, 1.0);
            else 
                playback.set_scaling(CHANNEL_REAR_LEFT, 0.0);
            
            if (dir & SND_DIRECTION_RIGHT)
                playback.set_scaling(CHANNEL_REAR_RIGHT, 1.0);
            else 
                playback.set_scaling(CHANNEL_REAR_RIGHT, 0.0);

            playback.device = sound_output_device;
            clip.file = stimuli_dir() + "sounds/" + item.snd_fn + ".wav";

            start(t);
            return expected_start_time;
        }
    }

    void done(int message)
    {
        audio.abort();
        signal_target(message);
        layout.reset();
        max_trial_dur_timer.abort();
        abort_trial_timer.abort();
        control.erase_clock();
        control.hide_speaker_icon(FRONT_SIDE | LEFT_SIDE | RIGHT_SIDE);
    }

    void setup(TestItem[] items)
    {
        // Load first audio file here.
        item_list   = items;
        item        = item_list[random(0, item_list.size)];

        // preload the file
        audio.clip.file = stimuli_dir() + "sounds/" + item.snd_fn + ".wav";
    }

    time action(Object      caller,
                time        start,
                int         trial_num,
                bool        shuffle_colors = false 
                )
    {
        time tref      = test_window1.show_test_page(this, start);
        nth_trial      = trial_num;        
        // Doesn't work because of std_baby_windows1.zm
        // test_window1.activate(); // to capture keyboard events.
        this.target    = caller;

        audio.play(tref, SND_DIRECTION_FRONT);

        response.start(tref);
        layout.start_stimulus(tref);

        control.target = this;
        control.reset_clock();
        control.begin_clock();
        control.start_clock(tref);

        if (MAX_TRIAL_DURATION > 0s)
            max_trial_dur_timer.start(tref + MAX_TRIAL_DURATION);

        return tref;
    }
}


